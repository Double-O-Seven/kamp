import org.gradle.internal.jvm.Jvm

configurations {
    runtimeCodegen
}

apply plugin: 'kamp-cpp-codegen'

dependencies {
    runtimeCodegen project(':kamp-core')
}

def runtimePackageName = "ch.leadrian.samp.kamp.core.runtime"

def sampgdkDir = "$projectDir/src/main/cpp/sampgdk"

def idlFilesDir = "$sampgdkDir/lib/sampgdk"

def actorIDLFile = "$idlFilesDir/a_actor.idl"
def objectsIDLFile = "$idlFilesDir/a_objects.idl"
def playersIDLFile = "$idlFilesDir/a_players.idl"
def sampIDLFile = "$idlFilesDir/a_samp.idl"
def vehiclesIDLFile = "$idlFilesDir/a_vehicles.idl"

def kampSrcDir = "$sampgdkDir/plugins/kamp"

def sampPluginSdkDir = "$projectDir/src/main/cpp/samp-plugin-sdk-original"

def sampgdkBuildDir = "$sampgdkDir/build"

def kampCoreMainOutput = project(':kamp-core').sourceSets.main.output

kampCppCodegen {
    runtimeJavaPackageName = runtimePackageName
    outputDirectoryPath = kampSrcDir
    interfaceDefinitionFiles actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
}

task generateSAMPNativeFunctionsHeaderFile {
    def sampNativeFunctionsHeaderFile = "$kampSrcDir/SAMPNativeFunctions.h"
    dependsOn ':kamp-core:classes'
    inputs.dir kampCoreMainOutput
    outputs.file sampNativeFunctionsHeaderFile
    doLast {
        file(sampNativeFunctionsHeaderFile).parentFile.mkdirs()
        exec {
            executable Jvm.current().getExecutable('javah')
            args = [
                    "-o", sampNativeFunctionsHeaderFile,
                    "-classpath", kampCoreMainOutput.asPath,
                    "-jni",
                    "-verbose",
                    "${runtimePackageName}.SAMPNativeFunctions"
            ]
        }
    }
}

task makePlugin {
    dependsOn(
            generateSAMPNativeFunctionsHeaderFile,
            generateKampCppFiles
    )

    doFirst {
        mkdir sampgdkBuildDir
    }

    doLast {
        exec {
            workingDir sampgdkBuildDir
            if (System.getProperty("os.name").toLowerCase().contains("windows")) {
                commandLine 'cmd', '/c', "cmake .. -DSAMP_SDK_ROOT=${sampPluginSdkDir} -DSAMPGDK_STATIC=ON -DSAMPGDK_BUILD_PLUGINS=ON"
            } else {
                commandLine "cmake .. -DSAMP_SDK_ROOT=${sampgdkBuildDir}"
            }
        }

        exec {
            workingDir sampgdkBuildDir
            if (System.getProperty("os.name").toLowerCase().contains("windows")) {
                commandLine 'cmd', '/c', 'cmake --build . --config Release'
            } else {
                commandLine 'cmake --build . --config Release'
            }
        }
    }

    inputs.dir kampSrcDir
    outputs.dir sampgdkBuildDir
}

clean.dependsOn cleanGenerateKampCppFiles
clean.dependsOn cleanMakePlugin
clean.dependsOn cleanGenerateSAMPNativeFunctionsHeaderFile

build.dependsOn makePlugin
