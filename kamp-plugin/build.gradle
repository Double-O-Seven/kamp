configurations {
    runtimeCodegen
}

dependencies {
    runtimeCodegen project(':kamp-runtime-codegen')
}

def runtimePackageName = "ch.leadrian.samp.kamp.runtime"

def sampgdkDir = "${projectDir}/src/main/cpp/sampgdk"

def idlFilesDir = "${sampgdkDir}/lib/sampgdk"

def actorIDLFile = "${idlFilesDir}/a_actor.idl"
def objectsIDLFile = "${idlFilesDir}/a_objects.idl"
def playersIDLFile = "${idlFilesDir}/a_players.idl"
def sampIDLFile = "${idlFilesDir}/a_samp.idl"
def vehiclesIDLFile = "${idlFilesDir}/a_vehicles.idl"

def kampSrcDir = "${sampgdkDir}/plugins/kamp"

def sampPluginSdkDir = "${projectDir}/src/main/cpp/samp-plugin-sdk-original"

def sampgdkBuildDir = "${sampgdkDir}/build"

def sampNativeFunctionsHeaderFile = "${kampSrcDir}/SAMPNativeFunctions.h"

task generateSAMPNativeFunctionsHeaderFile(type: JavaExec) {
    dependsOn ':kamp-runtime-codegen:classes'
    main "ch.leadrian.samp.kamp.runtimecodegen.SAMPNativeFunctionsHeaderCodeGenerator"
    classpath configurations.runtimeCodegen
    args = [
            "-o", kampSrcDir,
            "-p", runtimePackageName,
            "-i", actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
    ]
    inputs.files(actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile)
    outputs.file sampNativeFunctionsHeaderFile
}

def sampNativeFunctionsCppFile = "${kampSrcDir}/SAMPNativeFunctions.cpp"

task generateSAMPNativeFunctionsCppFile(type: JavaExec) {
    dependsOn ':kamp-runtime-codegen:classes'
    main "ch.leadrian.samp.kamp.runtimecodegen.SAMPNativeFunctionsCppCodeGenerator"
    classpath configurations.runtimeCodegen
    args = [
            "-o", kampSrcDir,
            "-p", runtimePackageName,
            "-i", actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
    ]
    inputs.files(actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile)
    outputs.file sampNativeFunctionsCppFile
}

task makePlugin {
    dependsOn(generateSAMPNativeFunctionsHeaderFile, generateSAMPNativeFunctionsCppFile)

    doFirst {
        mkdir sampgdkBuildDir
    }

    doLast {
        exec {
            workingDir sampgdkBuildDir
            if (System.getProperty("os.name").toLowerCase().contains("windows")) {
                commandLine 'cmd', '/c', "cmake .. -DSAMP_SDK_ROOT=${sampPluginSdkDir} -DSAMPGDK_STATIC=ON -DSAMPGDK_BUILD_PLUGINS=ON"
            } else {
                commandLine "cmake .. -DSAMP_SDK_ROOT=${sampgdkBuildDir}"
            }
        }

        exec {
            workingDir sampgdkBuildDir
            if (System.getProperty("os.name").toLowerCase().contains("windows")) {
                commandLine 'cmd', '/c', 'cmake --build . --config Release'
            } else {
                commandLine 'cmake --build . --config Release'
            }
        }
    }

    inputs.dir kampSrcDir
    outputs.dir sampgdkBuildDir
}

clean {
    delete += sampgdkBuildDir
    delete += sampNativeFunctionsHeaderFile
    delete += sampNativeFunctionsCppFile
}

build.dependsOn makePlugin
