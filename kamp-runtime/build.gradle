apply plugin: 'maven'

configurations {
    runtimeCodegen
}

dependencies {

    compileOnly(
            'com.google.inject:guice:4.2.0'
    )

    runtimeCodegen project(':kamp-runtime-codegen')
}

def generatedSrcDir = "${buildDir}/generated-src/main/java"

sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

def runtimePackageName = "ch.leadrian.samp.kamp.runtime"

def idlFilesDir = "${project(':kamp-plugin').projectDir}/src/main/cpp/sampgdk/lib/sampgdk"

def actorIDLFile = "${idlFilesDir}/a_actor.idl"
def objectsIDLFile = "${idlFilesDir}/a_objects.idl"
def playersIDLFile = "${idlFilesDir}/a_players.idl"
def sampIDLFile = "${idlFilesDir}/a_samp.idl"
def vehiclesIDLFile = "${idlFilesDir}/a_vehicles.idl"

def generatedSAMPConstantsJavaFile = "${generatedSrcDir}/ch/leadrian/samp/kamp/runtime/SAMPConstants.java"

task generateSAMPConstantsClass(type: JavaExec) {
    dependsOn ':kamp-runtime-codegen:classes'
    main "ch.leadrian.samp.kamp.runtimecodegen.SAMPConstantsJavaCodeGenerator"
    classpath configurations.runtimeCodegen
    args = [
            "-o", generatedSrcDir,
            "-p", runtimePackageName,
            "-i", actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
    ]
    inputs.files(actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile)
    outputs.file generatedSAMPConstantsJavaFile
}

def generatedSAMPCallbacksJavaFile = "${generatedSrcDir}/ch/leadrian/samp/kamp/runtime/SAMPCallbacks.java"

task generateSAMPCallbacksClass(type: JavaExec) {
    dependsOn ':kamp-runtime-codegen:classes'
    main "ch.leadrian.samp.kamp.runtimecodegen.SAMPCallbacksJavaCodeGenerator"
    classpath configurations.runtimeCodegen
    args = [
            "-o", generatedSrcDir,
            "-p", runtimePackageName,
            "-i", actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
    ]
    inputs.files(actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile)
    outputs.file generatedSAMPCallbacksJavaFile
}

def generatedSAMPNativeFunctionsJavaFile = "${generatedSrcDir}/ch/leadrian/samp/kamp/runtime/SAMPNativeFunctions.java"

task generateSAMPNativeFunctionsClass(type: JavaExec) {
    dependsOn ':kamp-runtime-codegen:classes'
    main "ch.leadrian.samp.kamp.runtimecodegen.SAMPNativeFunctionsJavaCodeGenerator"
    classpath configurations.runtimeCodegen
    args = [
            "-o", generatedSrcDir,
            "-p", runtimePackageName,
            "-i", actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile
    ]
    inputs.files(actorIDLFile, objectsIDLFile, playersIDLFile, sampIDLFile, vehiclesIDLFile)
    outputs.file generatedSAMPNativeFunctionsJavaFile
}

compileKotlin.dependsOn(generateSAMPConstantsClass, generateSAMPCallbacksClass, generateSAMPNativeFunctionsClass)
compileJava.dependsOn(generateSAMPConstantsClass, generateSAMPCallbacksClass, generateSAMPNativeFunctionsClass)
